@Program        ::= Block
@Block          ::= Statements
Statements      ::= Statement StatementEnd Statements
                ::= ""
Statement       ::= LineStatement
                ::= BlockStatement
                ::= ""
LineStatement   ::= PrintStatement
                ::= SetStatement
                ::= IncStatement
                ::= InputStatement
BlockStatement  ::= WhileStatement
                ::= ForStatement
                ::= IfStatement
@PrintStatement ::= "print" ExprList SuppressNewline
SuppressNewline ::= @"..."
                ::= ""
@SetStatement   ::= "set" NameThing "to" Expression
@IncStatement   ::= "inc" NameThing
@InputStatement ::= "input" NameThing AsPart
AsPart          ::= "as" NameThing
                ::= ""
@WhileStatement ::= "while" Expression HeaderEnd Block "repeat"
@ForStatement   ::= "for" NameThing "from" Expression "to" Expression HeaderEnd Block "next"
@IfStatement    ::= "if" Expression HeaderEnd Block ElseBlocks "end" "if"
ElseBlocks      ::= "else" ElseIfsOrBlock
                ::= ""
ElseIfsOrBlock  ::= "if" Expression HeaderEnd Block ElseBlocks
                ::= HeaderEnd Block
@Expression     ::= Atom BinaryRest
                ::= UnaryOperator Atom
BinaryRest      ::= BinaryOperator Atom
                ::= ""
BinaryOperator  ::= @"+"
                ::= @"-"
                ::= @"*"
                ::= @"/"
                ::= @"mod"
                ::= @"<"
                ::= @">"
                ::= @"="
                ::= @"<="
                ::= @">="
                ::= @"\="
                ::= @"|"
                ::= @"||"
                ::= @"and"
                ::= @"or"
UnaryOperator   ::= @"-"
                ::= @"/"
                ::= @"not"
Atom            ::= Literal
                ::= NameThing
                ::= @"random"
                ::= "(" Expression ")"
@NameThing      ::= @<name> NameThingMore
NameThingMore   ::= Parentheses NameThingMore
                ::= SquareBraces NameThingMore
                ::= ""
@Parentheses    ::= "(" ExprList ")"
@SquareBraces   ::= "[" Expression OptSlice "]"
OptSlice        ::= "..." Expression
                ::= ""
ExprList        ::= Expression ExprListMore
                ::= ""
ExprListMore    ::= "," Expression ExprListMore
                ::= ""
Literal         ::= @<integer>
                ::= @<boolean>
                ::= @<character>
                ::= @<string>
StatementEnd    ::= <eol>
                ::= ";"
HeaderEnd       ::= <eol>
                ::= ":"
